apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.eeka.matstoremanager"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        yudu {
//            applicationId "com.eeka.mespad.yd"
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_channel", "YD"
            resValue "string", "system_code", "P"
        }
        longhua {
//            applicationId "com.eeka.mespad.lh"
            resValue "string", "app_channel", "LH"
            resValue "string", "system_code", "Q"
        }
    }

    flavorDimensions 'api'
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    //指定打包后应用名称
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "MatStoreManager_${variant.versionName}_${variant.versionCode}_${variant.productFlavors[0].name}.apk"
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':OkHttpFinal')
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.4'
}
